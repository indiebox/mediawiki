developer=http://wikimediafoundation.org/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
_majorpkgver=1.30
pkgver=${_majorpkgver}.0
pkgrel=1
pkgdesc="The wiki that Wikipedia runs on"
arch=('any')
license=("GPL2")
source=("http://releases.wikimedia.org/mediawiki/${_majorpkgver}/mediawiki-${pkgver}.tar.gz")
releasepage=("https://www.mediawiki.org/wiki/MediaWiki")
depends=(
    'php'
    'smtp-server'
)
options=('!strip')
sha512sums=('53c6ca82280938d1e3281aa296f44c86dcfbbdf82710b7de578e73e1ef3150db145e059c8c8208859bc437f7a7f7a13eed896be9d44fd364a0ee6d78893fbe86')

package() {
# Manifest
    mkdir -p $pkgdir/var/lib/ubos/manifests
    install -m644 $startdir/ubos-manifest.json $pkgdir/var/lib/ubos/manifests/${pkgname}.json

# Icons
    mkdir -p $pkgdir/srv/http/_appicons/$pkgname
    install -m644 $startdir/appicons/{72x72,144x144}.png $pkgdir/srv/http/_appicons/$pkgname/

# Code
    mkdir -p $pkgdir/usr/share/${pkgname}/bin
    install -m755 $startdir/bin/{create-admin,symlink-logo,upgrade}.pl $pkgdir/usr/share/${pkgname}/bin/

# Templates
    mkdir -p $pkgdir/usr/share/${pkgname}/tmpl
    install -m644 $startdir/tmpl/{AdminSettings.php.tmpl,LocalSettings.php.tmpl,htaccess.pl} $pkgdir/usr/share/${pkgname}/tmpl/

# Source
    mkdir -p $pkgdir/usr/share/${pkgname}/mediawiki
    for f in $startdir/src/mediawiki-${pkgver}/*; do
        [ "$(basename $f)" != "tests" ] && [ "$(basename $f)" != "docs" ] && cp -dr --no-preserve=ownership $f $pkgdir/usr/share/${pkgname}/mediawiki/
    done

# Cache
    mkdir -p $pkgdir/var/cache/${pkgname}
}
