developer=http://wikimediafoundation.org/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
_majorpkgver=1.25
pkgver=${_majorpkgver}.3
pkgrel=1
pkgdesc="The wiki that Wikipedia runs on"
arch=('any')
license=("GPL")
source=("http://releases.wikimedia.org/mediawiki/${_majorpkgver}/mediawiki-${pkgver}.tar.gz")
options=('!strip')
sha512sums=('1e9ba10252cac95ca6ccf9a3008e84c735baf69db63ef050e5238909c97e707c48e2d408118793bc451b10469f65ef1e82dfc9d247e08ca085a2fd9e9f9ecc8c')

package() {
# Manifest
    mkdir -p $pkgdir/var/lib/ubos/manifests
    install -m644 $startdir/ubos-manifest.json $pkgdir/var/lib/ubos/manifests/${pkgname}.json

# Icons
    mkdir -p $pkgdir/srv/http/_appicons/$pkgname
    install -m644 $startdir/appicons/{72x72,144x144}.png $pkgdir/srv/http/_appicons/$pkgname/

# Code
    mkdir -p $pkgdir/usr/share/${pkgname}/bin
    install -m755 $startdir/bin/{create-admin,symlink-logo,upgrade}.pl $pkgdir/usr/share/${pkgname}/bin/

# Templates
    mkdir -p $pkgdir/usr/share/${pkgname}/tmpl
    install -m644 $startdir/tmpl/{AdminSettings.php.tmpl,LocalSettings.php.tmpl,htaccess.pl} $pkgdir/usr/share/${pkgname}/tmpl/

# Source
    mkdir -p $pkgdir/usr/share/${pkgname}/mediawiki
    for f in $startdir/src/mediawiki-${pkgver}/*; do
        [ "$(basename $f)" != "tests" ] && [ "$(basename $f)" != "docs" ] && cp -dr --no-preserve=ownership $f $pkgdir/usr/share/${pkgname}/mediawiki/
    done

# Cache
    mkdir -p $pkgdir/var/cache/${pkgname}
}
