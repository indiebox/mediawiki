developer=http://wikimediafoundation.org/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
_majorpkgver=1.28
pkgver=${_majorpkgver}.0
pkgrel=1
pkgdesc="The wiki that Wikipedia runs on"
arch=('any')
license=("GPL")
source=("http://releases.wikimedia.org/mediawiki/${_majorpkgver}/mediawiki-${pkgver}.tar.gz")
depends=(
    'php'
    'smtp-server'
)
options=('!strip')
sha512sums=('0bf3df06af8251729e9c01106a3eea7bdb4b6f27388501c627cb714cbff722f408ade725d7223ab69e79e036cf2a09b1f9a0659f45898508347638b50d440cda')

package() {
# Manifest
    mkdir -p $pkgdir/var/lib/ubos/manifests
    install -m644 $startdir/ubos-manifest.json $pkgdir/var/lib/ubos/manifests/${pkgname}.json

# Icons
    mkdir -p $pkgdir/srv/http/_appicons/$pkgname
    install -m644 $startdir/appicons/{72x72,144x144}.png $pkgdir/srv/http/_appicons/$pkgname/

# Code
    mkdir -p $pkgdir/usr/share/${pkgname}/bin
    install -m755 $startdir/bin/{create-admin,symlink-logo,upgrade}.pl $pkgdir/usr/share/${pkgname}/bin/

# Templates
    mkdir -p $pkgdir/usr/share/${pkgname}/tmpl
    install -m644 $startdir/tmpl/{AdminSettings.php.tmpl,LocalSettings.php.tmpl,htaccess.pl} $pkgdir/usr/share/${pkgname}/tmpl/

# Source
    mkdir -p $pkgdir/usr/share/${pkgname}/mediawiki
    for f in $startdir/src/mediawiki-${pkgver}/*; do
        [ "$(basename $f)" != "tests" ] && [ "$(basename $f)" != "docs" ] && cp -dr --no-preserve=ownership $f $pkgdir/usr/share/${pkgname}/mediawiki/
    done

# Cache
    mkdir -p $pkgdir/var/cache/${pkgname}
}
