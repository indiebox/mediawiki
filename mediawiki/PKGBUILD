developer=http://wikimediafoundation.org/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
_majorpkgver=1.31
pkgver=${_majorpkgver}.1
pkgrel=11
pkgdesc="The wiki that Wikipedia runs on"
arch=('any')
license=("GPL2")
source=("http://releases.wikimedia.org/mediawiki/${_majorpkgver}/mediawiki-${pkgver}.tar.gz")
releasepage=("https://www.mediawiki.org/wiki/MediaWiki")
depends=(
    'php'
    'smtp-server'
)
options=('!strip')
sha512sums=('ee49649cc37d0a7d45a7c6d90c822c2a595df290be2b5bf085affbec3318768700a458a6e5b5b7e437651400b9641424429d6d304f870c22ec63fae86ffc5152')

package() {
# Manifest
    install -D -m644 ${startdir}/ubos-manifest.json ${pkgdir}/ubos/lib/ubos/manifests/${pkgname}.json

# Icons
    install -D -m644 ${startdir}/appicons/{72x72,144x144}.png -t ${pkgdir}/ubos/http/_appicons/${pkgname}/

# Code
    install -D -m755 ${startdir}/bin/*.pl -t ${pkgdir}/ubos/share/${pkgname}/bin/

# Templates
    install -D -m644 ${startdir}/tmpl/{AdminSettings.php.tmpl,LocalSettings.php.tmpl,htaccess.pl} -t ${pkgdir}/ubos/share/${pkgname}/tmpl/

# Source
    mkdir -p ${pkgdir}/ubos/share/${pkgname}/mediawiki
    for f in ${startdir}/src/mediawiki-${pkgver}/*; do
        [ "$(basename $f)" != "tests" ] && [ "$(basename $f)" != "docs" ] && cp -dr --no-preserve=ownership $f ${pkgdir}/ubos/share/${pkgname}/mediawiki/
    done

# Cache
    mkdir -p ${pkgdir}/var/cache/${pkgname}
}
