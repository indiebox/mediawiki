developer=http://wikimediafoundation.org/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
_majorpkgver=1.28
pkgver=${_majorpkgver}.2
pkgrel=2
pkgdesc="The wiki that Wikipedia runs on"
arch=('any')
license=("GPL")
source=("http://releases.wikimedia.org/mediawiki/${_majorpkgver}/mediawiki-${pkgver}.tar.gz")
depends=(
    'php'
    'smtp-server'
)
options=('!strip')
sha512sums=('049fa132f17efa43c9cf380fdd85b117b959c8b70fd48f0c8e0dec8a770e3f3405fe272df006ba1fe47fa339b2eb4030ac21edc01295232219eeecdfebee08ce')

package() {
# Manifest
    mkdir -p $pkgdir/var/lib/ubos/manifests
    install -m644 $startdir/ubos-manifest.json $pkgdir/var/lib/ubos/manifests/${pkgname}.json

# Icons
    mkdir -p $pkgdir/srv/http/_appicons/$pkgname
    install -m644 $startdir/appicons/{72x72,144x144}.png $pkgdir/srv/http/_appicons/$pkgname/

# Code
    mkdir -p $pkgdir/usr/share/${pkgname}/bin
    install -m755 $startdir/bin/{create-admin,symlink-logo,upgrade}.pl $pkgdir/usr/share/${pkgname}/bin/

# Templates
    mkdir -p $pkgdir/usr/share/${pkgname}/tmpl
    install -m644 $startdir/tmpl/{AdminSettings.php.tmpl,LocalSettings.php.tmpl,htaccess.pl} $pkgdir/usr/share/${pkgname}/tmpl/

# Source
    mkdir -p $pkgdir/usr/share/${pkgname}/mediawiki
    for f in $startdir/src/mediawiki-${pkgver}/*; do
        [ "$(basename $f)" != "tests" ] && [ "$(basename $f)" != "docs" ] && cp -dr --no-preserve=ownership $f $pkgdir/usr/share/${pkgname}/mediawiki/
    done

# Cache
    mkdir -p $pkgdir/var/cache/${pkgname}
}
