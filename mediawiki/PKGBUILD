developer=http://wikimediafoundation.org/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
_majorpkgver=1.34
pkgver=${_majorpkgver}.0
pkgrel=1
pkgdesc="The wiki that Wikipedia runs on"
arch=('any')
license=("GPL2")
source=("http://releases.wikimedia.org/mediawiki/${_majorpkgver}/mediawiki-${pkgver}.tar.gz")
releasepage=("https://www.mediawiki.org/wiki/MediaWiki")
depends=(
    'php'
    'smtp-server'
)
options=('!strip')
sha512sums=('b6b1aeec26a1c114eeec0bdf18d4b3160fe02dac2920a39a045acb74e62aa8f8a28e6a81c01fedba7976e4dd0c96463e0f1badfddd3015eef9197b01586a236d')

package() {
# Manifest
    install -D -m644 ${startdir}/ubos-manifest.json ${pkgdir}/ubos/lib/ubos/manifests/${pkgname}.json

# Icons
    install -D -m644 ${startdir}/appicons/{72x72,144x144}.png -t ${pkgdir}/ubos/http/_appicons/${pkgname}/

# Code
    install -D -m755 ${startdir}/bin/*.pl -t ${pkgdir}/ubos/share/${pkgname}/bin/

# Templates
    install -D -m644 ${startdir}/tmpl/{AdminSettings.php.tmpl,LocalSettings.php.tmpl,htaccess.pl} -t ${pkgdir}/ubos/share/${pkgname}/tmpl/

# Source
    mkdir -p ${pkgdir}/ubos/share/${pkgname}/mediawiki
    for f in ${startdir}/src/mediawiki-${pkgver}/*; do
        [ "$(basename $f)" != "tests" ] && [ "$(basename $f)" != "docs" ] && cp -dr --no-preserve=ownership $f ${pkgdir}/ubos/share/${pkgname}/mediawiki/
    done

# Cache
    mkdir -p ${pkgdir}/var/cache/${pkgname}
}
